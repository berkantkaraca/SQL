--MODULE 10 TRY IT YOURSELF
---------------------------


--Chapter 10,“Exceptions: Advanced Concepts”

/*
1) Modify the script you created in project 1 of the “Try It Yourself” section in Chapter 9. Raise a userdefined
exception with the RAISE_APPLICATION_ERROR statement.Otherwise, display how many
students are in a section.Make sure your program can process all sections.

ANSWER: The script should look similar to the following. Changes are shown in bold.
*/


SET SERVEROUTPUT ON
DECLARE
  v_section_id     NUMBER := &sv_section_id;
  v_total_students NUMBER;
BEGIN
  -- Calculate number of students enrolled
  SELECT COUNT(*)
  INTO v_total_students
  FROM enrollment
  WHERE section_id     = v_section_id;
  IF v_total_students >= 10 THEN
    RAISE_APPLICATION_ERROR (-20000, 'There are too many students for '|| 'section '||v_section_id);
  ELSE
    DBMS_OUTPUT.PUT_LINE ('There are '||v_total_students|| ' students for section ID: '||v_section_id);
  END IF;
END;

/*
In this version of the script, you use the RAISE_APPLICATION_ERROR statement to handle the
following error condition: If the number of students enrolled in a particular section is equal to or
greater than ten, an error is raised. It is important to remember that the RAISE_APPLICATION_
ERROR statement works with the unnamed user-defined exceptions.Therefore, notice that there is
no reference to the exception e_too_many_students anywhere in this script. On the other
hand, an error number has been associated with the error message.
When run, this exercise produces the following output (the same section IDs are used for this
script as well: 101, 116, and 999):
Enter value for sv_section_id: 101
old 2: v_section_id NUMBER := &sv_section_id;
new 2: v_section_id NUMBER := 101;
DECLARE
*
ERROR at line 1:
ORA-20000: There are too many students for section 101
ORA-06512: at line 12
Enter value for sv_section_id: 116
old 2: v_section_id NUMBER := &sv_section_id;
new 2: v_section_id NUMBER := 116;
There are 8 students for section ID: 116
PL/SQL procedure successfully completed.
Enter value for sv_section_id: 999
old 2: v_section_id NUMBER := &sv_section_id;
new 2: v_section_id NUMBER := 999;
There are 0 students for section ID: 999
PL/SQL procedure successfully completed.



2) Create the following script: Try to add a record to the INSTRUCTOR table without providing values
for the columns CREATED_BY, CREATED_DATE, MODIFIED_BY, and MODIFIED_DATE.Define an
exception and associate it with the Oracle error number so that the error generated by the INSERT
statement is handled.

ANSWER: Consider the following script. Notice that it has no exception handlers:
*/

DECLARE
  v_first_name instructor.first_name%type := '&sv_first_name';
  v_last_name instructor.last_name%type   := '&sv_last_name';
BEGIN
  INSERT
  INTO instructor
    (
      instructor_id,
      first_name,
      last_name
    )
    VALUES
    (
      INSTRUCTOR_ID_SEQ.NEXTVAL,
      v_first_name,
      v_last_name
    );
  COMMIT;
END;


/*
In this version of the script, you are trying to add a new record to the INSTRUCTOR table.The
INSERT statement has only three columns: INSTRUCTOR_ID, FIRST_NAME, and LAST_NAME.The
value for the column INSTRUCTOR_ID is determined from the sequence INSTRUCTOR_ID_SEQ, and
the user provides the values for the columns FIRST_NAME and LAST_NAME.
When run, this script produces the following error message:
Enter value for sv_first_name: John
old 2: '&sv_first_name';
new 2: 'John';
Enter value for sv_last_name: Smith
old 3: '&sv_last_name';
new 3: 'Smith';
DECLARE
*
ERROR at line 1:
ORA-01400: cannot insert NULL into
("STUDENT"."INSTRUCTOR"."CREATED_BY")
ORA-06512: at line 5
This error message states that a NULL value cannot be inserted into the column CREATED_BY of
the INSTRUCTOR table.Therefore, you need to add an exception handler to the script, as follows.
Changes are shown in bold.
*/

SET SERVEROUTPUT ON
DECLARE
  v_first_name instructor.first_name%type := '&sv_first_name';
  v_last_name instructor.last_name%type   := '&sv_last_name';
  e_non_null_value EXCEPTION;
  PRAGMA EXCEPTION_INIT(e_non_null_value, -1400);
BEGIN
  INSERT
  INTO INSTRUCTOR
    (
      instructor_id,
      first_name,
      last_name
    )
    VALUES
    (
      INSTRUCTOR_ID_SEQ.NEXTVAL,
      v_first_name,
      v_last_name
    );
  COMMIT;
EXCEPTION
WHEN e_non_null_value THEN
  DBMS_OUTPUT.PUT_LINE ('A NULL value cannot be '|| 'inserted. Check constraints on the INSTRUCTOR table.');
END;

/*
In this version of the script, you declare a new exception called e_non_null_value. Next,
you associate an Oracle error number with this exception. As a result, you can add an exceptionhandling
section to trap the error generated by Oracle.
When run, the new version produces the following output:
Enter value for sv_first_name: John
old 2: '&sv_first_name';
new 2: 'John';
Enter value for sv_last_name: Smith
old 3: '&sv_last_name';
new 3: 'Smith';
A NULL value cannot be inserted. Check constraints on the
INSTRUCTOR table.
PL/SQL procedure successfully completed.



3) Modify the script you just created. Instead of declaring a user-defined exception, add the OTHERS
exception handler to the exception-handling section of the block.Then display the error number
and the error message on the screen.

ANSWER: The script should look similar to the following. Changes are shown in bold.
*/


SET SERVEROUTPUT ON
DECLARE
  v_first_name instructor.first_name%type := '&sv_first_name';
  v_last_name instructor.last_name%type   := '&sv_last_name';
BEGIN
  INSERT
  INTO INSTRUCTOR
    (
      instructor_id,
      first_name,
      last_name
    )
    VALUES
    (
      INSTRUCTOR_ID_SEQ.NEXTVAL,
      v_first_name,
      v_last_name
    );
  COMMIT;
EXCEPTION
WHEN OTHERS THEN
  DBMS_OUTPUT.PUT_LINE ('Error code: '||SQLCODE);
  DBMS_OUTPUT.PUT_LINE ('Error message: '|| SUBSTR(SQLERRM, 1, 200));
END;

/*
Notice that as long as the OTHERS exception handler is used, there is no need to associate an
Oracle error number with a user-defined exception. When run, this exercise produces the following
output:
Enter value for sv_first_name: John
old 2: '&sv_first_name';
new 2: 'John';
Enter value for sv_last_name: Smith
old 3: '&sv_last_name';
new 3: 'Smith';
Error code: -1400
Error message: ORA-01400: cannot insert NULL into
("STUDENT"."INSTRUCTOR"."CREATED_BY")
PL/SQL procedure successfully completed.
*/


