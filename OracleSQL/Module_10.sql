--MODULE 10
-----------


--Exceptions: Advanced Concepts
--------------------------------


--RAISE_APPLICATION_ERROR

--�?renci no negatif olamaz RAISE_APPLICATION_ERROR ile yaz?m?
DECLARE
  v_student_id student.student_id%type := &sv_student_id;
  v_total_courses NUMBER;
BEGIN
  IF v_student_id < 0 THEN
    RAISE_APPLICATION_ERROR (-20000, 'An id cannot be negative');
  ELSE
    SELECT COUNT(*)
    INTO v_total_courses
    FROM enrollment
    WHERE student_id = v_student_id;
    DBMS_OUTPUT.PUT_LINE ('The student is registered for '|| v_total_courses||' courses');
  END IF;
END;


--Built-in exception ile RAISE_APPLICATION_ERROR kullan?m?
DECLARE
  v_student_id student.student_id%type := &sv_student_id;
  v_name VARCHAR2(50);
BEGIN
  SELECT first_name
    ||' '
    ||last_name
  INTO v_name
  FROM student
  WHERE student_id = v_student_id;
  DBMS_OUTPUT.PUT_LINE (v_name);
EXCEPTION
WHEN NO_DATA_FOUND THEN
  RAISE_APPLICATION_ERROR (-20001, 'This ID is invalid');
END;


--10.1.1 Use RAISE_APPLICATION_ERROR

--25 nolu kursun 89 nolu section'?nda ka� �?renci var?
-- ch10_1a.sql, version 1.0
SET SERVEROUTPUT ON
DECLARE
  v_students NUMBER(3) := 0;
BEGIN
  SELECT COUNT(*)
  INTO v_students
  FROM enrollment e,
    section s
  WHERE e.section_id = s.section_id
  AND s.course_no    = 25
  AND s.section_id   = 89;
  DBMS_OUTPUT.PUT_LINE ('Course 25, section 89 has '||v_students|| ' students');
END;


/*
B) Modify this script so that if a section of a course has more than ten students enrolled in it, an error
message is displayed, indicating that this course has too many students enrolled.
*/

SET SERVEROUTPUT ON
DECLARE
  v_students NUMBER(3) := 0;
BEGIN
  SELECT COUNT(*)
  INTO v_students
  FROM enrollment e,
    section s
  WHERE e.section_id = s.section_id
  AND s.course_no    = 25
  AND s.section_id   = 89;
  IF v_students      > 10 THEN
    RAISE_APPLICATION_ERROR (-20002, 'Course 25, section 89 has more than 10 students');
  END IF;
  DBMS_OUTPUT.PUT_LINE ('Course 25, section 89 has '||v_students|| ' students');
END;

-----------------------------------------------------------------------------------------------

--EXCEPTION_INIT Pragma: Numarası bilinen hataları özgün şekilde ele alma

--Integrity Constraint hatası alınır
-- parent tablosundan bir kolonun child kayıtları varken parent kayıt silinemez
DECLARE
  v_zip zipcode.zip%type := '&sv_zip';
BEGIN
  DELETE FROM zipcode WHERE zip = v_zip;
  DBMS_OUTPUT.PUT_LINE ('Zip '||v_zip||' has been deleted');
  COMMIT;
END;



--?lgili hata numaras? EXCEPTION_INIT pragmas? ile tan?mlan?r ve hata ele al?n?r

DECLARE
  v_zip zipcode.zip%type := '&sv_zip';
  e_child_exists EXCEPTION;
  PRAGMA EXCEPTION_INIT(e_child_exists, -2292);
BEGIN
  DELETE FROM zipcode WHERE zip = v_zip;
  DBMS_OUTPUT.PUT_LINE ('Zip '||v_zip||' has been deleted');
  COMMIT;
EXCEPTION
WHEN e_child_exists THEN
  DBMS_OUTPUT.PUT_LINE ('Delete students for this '|| 'zipcode first');
END;



--10.2.1 USE the EXCEPTION_INIT Pragma

-- ch10_2a.sql, version 1.0
SET SERVEROUTPUT ON
BEGIN
  INSERT
  INTO course
    (
      course_no,
      description,
      created_by,
      created_date
    )
    VALUES
    (
      COURSE_NO_SEQ.NEXTVAL,
      'TEST COURSE',
      USER,
      SYSDATE
    );
  COMMIT;
  DBMS_OUTPUT.PUT_LINE ('One course has been added');
END;


/*
A) What output is printed on the screen?


B) Explain why the script does not execute successfully.

ANSWER: The script does not execute successfully because a NULL is inserted for the
MODIFIED_BY and MODIFIED_DATE columns.


C) Add a user-defined exception to the script so that the error generated by the INSERT statement is
handled.
*/

-- ch10_2b.sql, version 2.0
SET SERVEROUTPUT ON
DECLARE
  e_constraint_violation EXCEPTION;
  PRAGMA EXCEPTION_INIT(e_constraint_violation, -1400);
BEGIN
  INSERT
  INTO course
    (
      course_no,
      description,
      created_by,
      created_date
    )
    VALUES
    (
      COURSE_NO_SEQ.NEXTVAL,
      'TEST COURSE',
      USER,
      SYSDATE
    );
  COMMIT;
  DBMS_OUTPUT.PUT_LINE ('One course has been added');
EXCEPTION
WHEN e_constraint_violation THEN
  DBMS_OUTPUT.PUT_LINE ('INSERT statement is '|| 'violating a constraint');
END;

---------------------------------------------------------------------------------------


--SQLCODE and SQLERRM

--OTHERS ile hata yakalan?r

DECLARE
  v_zip   VARCHAR2(5) := '&sv_zip';
  v_city  VARCHAR2(15);
  v_state CHAR(2);
BEGIN
  SELECT city, state INTO v_city, v_state FROM zipcode WHERE zip = v_zip;
  DBMS_OUTPUT.PUT_LINE (v_city||', '||v_state);
EXCEPTION
WHEN OTHERS THEN
  DBMS_OUTPUT.PUT_LINE ('An error has occurred');
END;



--SQLCODE ve SQLERRM eklenmi? hali

DECLARE
  v_zip      VARCHAR2(5) := '&sv_zip';
  v_city     VARCHAR2(15);
  v_state    CHAR(2);
  V_ERR_CODE NUMBER;
  v_err_msg  VARCHAR2(1500);
BEGIN
  SELECT city, state INTO v_city, v_state FROM zipcode WHERE zip = v_zip;
  DBMS_OUTPUT.PUT_LINE (v_city||', '||v_state);
EXCEPTION
WHEN OTHERS THEN
  v_err_code := SQLCODE;
  v_err_msg  := SQLERRM;
  DBMS_OUTPUT.PUT_LINE ('Error code: '||v_err_code);
  DBMS_OUTPUT.PUT_LINE ('Error message: '||V_ERR_MSG);
END;


--SQLCODE ve SQLERRM extra kullan?m ?ekilleri

BEGIN
  DBMS_OUTPUT.PUT_LINE ('Error code: '||SQLCODE);
  DBMS_OUTPUT.PUT_LINE ('Error message1: '||SQLERRM(SQLCODE));
  DBMS_OUTPUT.PUT_LINE ('Error message2: '||SQLERRM(100));
  DBMS_OUTPUT.PUT_LINE ('Error message3: '||SQLERRM(200));
  DBMS_OUTPUT.PUT_LINE ('Error message4: '||SQLERRM(-20000));
END;


--10.3.1 Use SQLCODE and SQLERRM


--Unique constraint hatas? al?n?r
-- ch10_3a.sql, version 1.0
SET SERVEROUTPUT ON
BEGIN
  INSERT
  INTO ZIPCODE
    (
      zip,
      city,
      state,
      created_by,
      created_date,
      modified_by,
      modified_date
    )
    VALUES
    (
      '10027',
      'NEW YORK',
      'NY',
      USER,
      SYSDATE,
      USER,
      SYSDATE
    );
  COMMIT;
END;


/*
B) Modify the script so that it completes successfully and so that the error number and message are
displayed on the screen.
*/

-- ch10_3b.sql, version 2.0
SET SERVEROUTPUT ON
BEGIN
  INSERT
  INTO ZIPCODE
    (
      zip,
      city,
      state,
      created_by,
      created_date,
      modified_by,
      modified_date
    )
    VALUES
    (
      '10027',
      'NEW YORK',
      'NY',
      USER,
      SYSDATE,
      USER,
      SYSDATE
    );
  COMMIT;
EXCEPTION
WHEN OTHERS THEN
  DECLARE
    v_err_code NUMBER        := SQLCODE;
    v_err_msg  VARCHAR2(100) := SUBSTR(SQLERRM, 1, 100);
  BEGIN
    DBMS_OUTPUT.PUT_LINE ('Error code: '||v_err_code);
    DBMS_OUTPUT.PUT_LINE ('Error message: '||v_err_msg);
  END;
END;










